plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
}

group = 'me.tibinonest.plugins'
version = '1.0.1'
description = 'A spigot plugin to set your world(s) to the current irl time.'

repositories {
    maven {
        url = uri('https://papermc.io/repo/repository/maven-public/')
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

java {
    withSourcesJar()
}

shadowJar {
    archiveClassifier.set('')
    relocate 'org.bstats', 'bstats'
    from 'LICENSE'
    minimize()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.version,
                'pluginDescription': project.description
        )
        include 'plugin.yml'
    }
}

assemble.dependsOn shadowJar
